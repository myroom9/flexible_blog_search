plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.hibernate.orm' version '6.1.7.Final'
    id 'org.graalvm.buildtools.native' version '0.9.20'
    id 'checkstyle'
}

allprojects {
    group = 'com.whahn'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

java.sourceCompatibility = JavaVersion.VERSION_17

subprojects {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.hibernate.orm"
    apply plugin: "org.graalvm.buildtools.native"
    apply plugin: "checkstyle"

    ext {
        set('springCloudVersion', "2022.0.1")
    }

    checkstyle {
        toolVersion = '10.2'
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    task checkstyle(type: Checkstyle) {
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        source 'src/main/java/'
        include '**/*.java'
        showViolations true
        classpath = files()
        ignoreFailures = false
        maxWarnings = 1
    }

    hibernate {
        enhancement {
            lazyInitialization true
            dirtyTracking true
            associationManagement true
        }
    }
}

["whahn"].each {
    def subProjectDir = new File(projectDir, it)
    subProjectDir.eachDir {dir->
        def projectName = ":${it}-${dir.name}"
        project(projectName){
            bootJar.enabled(true)
            jar.enabled(false)
        }
    }
}

